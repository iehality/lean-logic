import QL.FOL.deduction

-- Prenex normal form

universes u v

namespace fol
open_locale logic_symbol aclogic
open subformula

variables (L : language.{u}) (m n : â„•)

inductive pnf (m : â„•) : â„• â†’ Type u
| openformula {n} : Î  p : subformula L m n, p.is_open â†’ pnf n
| fal         {n} : pnf (n + 1) â†’ pnf n
| ex          {n} : pnf (n + 1) â†’ pnf n

variables {L m n}



namespace pnf

instance : has_univ_quantifier' (pnf L m) := âŸ¨@pnf.fal L mâŸ©

instance : has_exists_quantifier' (pnf L m) := âŸ¨@pnf.ex L mâŸ©

def to_str [âˆ€ n, has_to_string (L.fn n)] [âˆ€ n, has_to_string (L.pr n)] : Î  {n}, pnf L m n â†’ string
| n (openformula p _) := "[" ++ to_string p ++ "]"
| n (fal Ï†)           := "âˆ€" ++ to_str Ï†
| n (ex Ï†)            := "âˆƒ" ++ to_str Ï†

instance [âˆ€ n, has_to_string (L.fn n)] [âˆ€ n, has_to_string (L.pr n)] : has_to_string (pnf L m n) := âŸ¨@to_str L m _ _ nâŸ©

@[simp] def rank : Î  {n}, pnf L m n â†’ â„•
| n (openformula p hp) := 0
| n (fal Ï†)            := Ï†.rank + 1
| n (ex  Ï†)            := Ï†.rank + 1

@[simp] lemma rank_forall (Ï† : pnf L m (n + 1)) : rank (âˆ€'Ï†) = rank Ï† + 1 := by simp[has_univ_quantifier'.univ]

@[simp] lemma rank_exists (Ï† : pnf L m (n + 1)) : rank (âˆƒ'Ï†) = rank Ï† + 1 := by simp[has_exists_quantifier'.ex]

@[simp] lemma forall_inj (p q : pnf L m (n + 1)) : âˆ€'p = âˆ€'q â†” p = q := âŸ¨fal.inj, congr_arg _âŸ©

@[simp] lemma exists_inj (p q : pnf L m (n + 1)) : âˆƒ'p = âˆƒ'q â†” p = q := âŸ¨ex.inj, congr_arg _âŸ©

@[simp] def to_formula : Î  {n}, pnf L m n â†’ subformula L m n
| n (openformula p hp) := p
| n (fal Ï†)            := âˆ€'to_formula Ï†
| n (ex  Ï†)            := âˆƒ'to_formula Ï†

instance : has_coe (pnf L m n) (subformula L m n) := âŸ¨@to_formula L m nâŸ©

@[simp] lemma to_formula_forall (Ï† : pnf L m (n + 1)) : to_formula (âˆ€'Ï†) = âˆ€'(to_formula Ï†) := by simp[has_univ_quantifier'.univ]

@[simp] lemma to_formula_exists (Ï† : pnf L m (n + 1)) : to_formula (âˆƒ'Ï†) = âˆƒ'(to_formula Ï†) := by simp[has_exists_quantifier'.ex]

section mlift

@[simp] def mlift : Î  {n}, pnf L m n â†’ pnf L (m + 1) n
| n (openformula p hp) := openformula p.mlift (by simpa using hp)
| n (fal Ï†)            := fal (mlift Ï†)
| n (ex  Ï†)            := ex (mlift Ï†)

@[simp] lemma mlift_forall (Ï† : pnf L m (n + 1)) : mlift (âˆ€'Ï†) = âˆ€'(mlift Ï†) := by simp[has_univ_quantifier'.univ]

@[simp] lemma mlift_exists (Ï† : pnf L m (n + 1)) : mlift (âˆƒ'Ï†) = âˆƒ'(mlift Ï†) := by simp[has_exists_quantifier'.ex]

@[simp] lemma mlift_to_formula : âˆ€ {n} (Ï† : pnf L m n), Ï†.mlift.to_formula = ğ—Ÿ Ï†.to_formula
| _ (openformula p hp) := by simp
| _ (fal Ï†)            := by simp; exact mlift_to_formula Ï†
| _ (ex Ï†)             := by simp; exact mlift_to_formula Ï†

@[simp] lemma rank_mlift : âˆ€ {n} (Ï† : pnf L m n), rank (mlift Ï†) = rank Ï†
| n (openformula p hp) := by simp
| n (fal p) := by show (âˆ€'p).mlift.rank = p.fal.rank; simpa using rank_mlift p
| n (ex p)  := by show (âˆƒ'p).mlift.rank = p.ex.rank; simpa  using rank_mlift p

end mlift

section push

@[simp] def push : Î  {n}, pnf L m (n + 1) â†’ pnf L (m + 1) n
| n (openformula p hp) := openformula p.push (by simpa using hp)
| n (fal Ï†)            := fal (push Ï†)
| n (ex  Ï†)            := ex (push Ï†)
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.rank)âŸ©]}

@[simp] lemma push_forall (Ï† : pnf L m (n + 1 + 1)) : push (âˆ€'Ï†) = âˆ€'(push Ï†) := by simp[has_univ_quantifier'.univ]

@[simp] lemma push_exists (Ï† : pnf L m (n + 1 + 1)) : push (âˆƒ'Ï†) = âˆƒ'(push Ï†) := by simp[has_exists_quantifier'.ex]

@[simp] lemma push_to_formula : âˆ€ {n} (Ï† : pnf L m (n + 1)), Ï†.push.to_formula = ğ—  Ï†.to_formula
| _ (openformula p hp) := by simp
| _ (fal Ï†)            := by simp; exact push_to_formula Ï†
| _ (ex Ï†)             := by simp; exact push_to_formula Ï†
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.rank)âŸ©]}

@[simp] lemma rank_push : âˆ€ {n} (Ï† : pnf L m (n + 1)), rank (push Ï†) = rank Ï†
| n (openformula p hp) := by simp
| n (fal p) := by show (âˆ€'p).push.rank = p.fal.rank; simpa using rank_push p
| n (ex p) := by show (âˆƒ'p).push.rank = p.ex.rank; simpa using rank_push p
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.rank)âŸ©]}

end push

section pull

@[simp] def pull : Î  {n}, pnf L (m + 1) n â†’ pnf L m (n + 1)
| n (openformula p hp) := openformula p.pull (by simpa using hp)
| n (fal Ï†)            := fal (pull Ï†)
| n (ex  Ï†)            := ex (pull Ï†)

@[simp] lemma pull_forall (Ï† : pnf L (m + 1) (n + 1)) : pull (âˆ€'Ï†) = âˆ€'(pull Ï†) := by simp[has_univ_quantifier'.univ]

@[simp] lemma pull_exists (Ï† : pnf L (m + 1) (n + 1)) : pull (âˆƒ'Ï†) = âˆƒ'(pull Ï†) := by simp[has_exists_quantifier'.ex]

@[simp] lemma pull_to_formula : âˆ€ {n} (Ï† : pnf L (m + 1) n), Ï†.pull.to_formula = ğ—¡ Ï†.to_formula
| _ (openformula p hp) := by simp
| _ (fal Ï†)            := by simp; exact pull_to_formula Ï†
| _ (ex Ï†)             := by simp; exact pull_to_formula Ï†

@[simp] lemma pull_push : âˆ€ {n} (Ï† : pnf L (m + 1) n), Ï†.pull.push = Ï†
| _ (openformula p hp) := by simp
| _ (fal Ï†)            := by simpa using pull_push Ï†
| _ (ex Ï†)             := by simpa using pull_push Ï†

end pull

section dummy

def dummy : pnf L m n â†’ pnf L m (n + 1) := pull âˆ˜ mlift

@[simp] lemma push_dummy (Ï† : pnf L m n) : push (dummy Ï†) = mlift Ï† :=
by simp[dummy]

lemma dummy_openformula (p : subformula L m n) (hp) :
  dummy (openformula p hp) = openformula p.dummy (by simpa using hp) := by simp[dummy]; refl

@[simp] lemma dummy_forall (Ï† : pnf L m (n + 1)) : dummy (âˆ€'Ï†) = âˆ€'(dummy Ï†) := by simp[dummy]

@[simp] lemma dummy_exists (Ï† : pnf L m (n + 1)) : dummy (âˆƒ'Ï†) = âˆƒ'(dummy Ï†) := by simp[dummy]

@[simp] lemma dummy_to_formula (Ï† : pnf L m n) : Ï†.dummy.to_formula = ğ—— Ï†.to_formula :=
by simp[mlift_to_formula, pull_to_formula, dummy, subformula.dummy]

@[simp] lemma rank_dummy : âˆ€ {n} (Ï† : pnf L m n), rank (dummy Ï†) = rank Ï†
| n (openformula p hp) := by simp[dummy_openformula]
| n (fal p) := by show (âˆ€'p).dummy.rank = p.fal.rank; simpa using rank_dummy p
| n (ex p) := by show (âˆƒ'p).dummy.rank = p.ex.rank; simpa using rank_dummy p

end dummy

@[simp] def neg : Î  {m n}, pnf L m n â†’ pnf L m n
| m n (openformula p hp) := openformula (âˆ¼p) (by simpa[is_open] using hp)
| m n (fal Ï†)            := âˆƒ'(pull $ neg $ push Ï†)
| m n (ex Ï†)             := âˆ€'(pull $ neg $ push Ï†)
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.2.rank)âŸ©]}

@[simp] def imply : Î  {m n}, pnf L m n â†’ pnf L m n â†’ pnf L m n
| m n (openformula p hp) (openformula q hq) := openformula (p âŸ¶ q) (by simp; exact âŸ¨hp, hqâŸ©)
| m n (openformula p hp) (fal Ïˆ)            := âˆ€'pull (imply (mlift $ openformula p hp) (push Ïˆ))
| m n (openformula p hp) (ex Ïˆ)             := âˆƒ'pull (imply (mlift $ openformula p hp) (push Ïˆ))
| m n (fal Ï†)            Ïˆ                  := âˆƒ'pull (imply (push Ï†) (mlift Ïˆ))
| m n (ex Ï†)             Ïˆ                  := âˆ€'pull (imply (push Ï†) (mlift Ïˆ))
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.2.1.rank + x.2.2.2.rank)âŸ©]}

open axiomatic_classical_logic' axiomatic_classical_logic provable

lemma equiv_to_formula_neg : âˆ€ {m} (T : preTheory L m) (p : pnf L m 0), T âŠ¢ (p.neg).to_formula âŸ· âˆ¼p.to_formula
| m T (openformula p hp) := by simp
| m T (pnf.fal Ï†) :=
    begin
      simp, show T âŠ¢ âˆƒ'ğ—¡ Ï†.push.neg.to_formula âŸ· âˆ¼âˆ€'Ï†.to_formula,
      have : ğ—Ÿ'T âŠ¢ Ï†.push.neg.to_formula âŸ· âˆ¼ğ—  Ï†.to_formula, by simpa using equiv_to_formula_neg ğ—Ÿ'T Ï†.push,
      have : T âŠ¢ âˆƒ'ğ—¡ Ï†.push.neg.to_formula âŸ· âˆƒ'âˆ¼Ï†.to_formula, by simpa using equiv_exists_of_equiv' this,
      refine equiv_trans this (equiv_symm $ neg_forall_pnf _)
    end
| m T (pnf.ex Ï†) :=
    begin
      simp, show T âŠ¢ âˆ€'ğ—¡ Ï†.push.neg.to_formula âŸ· âˆ¼âˆƒ'Ï†.to_formula,
      have : ğ—Ÿ'T âŠ¢ Ï†.push.neg.to_formula âŸ· âˆ¼ğ—  Ï†.to_formula, by simpa using equiv_to_formula_neg ğ—Ÿ'T Ï†.push,
      have : T âŠ¢ âˆ€'ğ—¡ Ï†.push.neg.to_formula âŸ· âˆ€'âˆ¼Ï†.to_formula, by simpa using equiv_forall_of_equiv' this,
      refine equiv_trans this (equiv_symm $ neg_exists_pnf _)
    end
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.2.rank)âŸ©]}

lemma equiv_to_formula_imply : âˆ€ {m} (T : preTheory L m) (p q : pnf L m 0),
  T âŠ¢ (p.imply q).to_formula âŸ· (p.to_formula âŸ¶ q.to_formula)
| m T (openformula p hp) (openformula q hq) := by simp
| m T (openformula p hp) (fal Ïˆ)            :=
    let Ï† := openformula p hp in
    begin
      simp, show T âŠ¢ (âˆ€'ğ—¡ (Ï†.mlift.imply Ïˆ.push).to_formula) âŸ· (p âŸ¶ âˆ€'Ïˆ.to_formula),
      have : ğ—Ÿ'T âŠ¢ (Ï†.mlift.imply Ïˆ.push).to_formula âŸ· (ğ—Ÿ p âŸ¶ ğ—  Ïˆ.to_formula),
      by simpa using equiv_to_formula_imply ğ—Ÿ'T Ï†.dummy.push Ïˆ.push,
      have : T âŠ¢ âˆ€'ğ—¡ (Ï†.mlift.imply Ïˆ.push).to_formula âŸ· âˆ€'(ğ—— p âŸ¶ Ïˆ.to_formula),
      by simpa using equiv_forall_of_equiv' this,
      refine equiv_trans this (equiv_symm $ imply_forall_pnf _ _)
    end
| m T (openformula p hp) (ex Ïˆ)             :=
    let Ï† := openformula p hp in
    begin
      simp, show T âŠ¢ (âˆƒ'ğ—¡ (Ï†.mlift.imply Ïˆ.push).to_formula) âŸ· (p âŸ¶ âˆƒ'Ïˆ.to_formula),
      have : ğ—Ÿ'T âŠ¢ (Ï†.mlift.imply Ïˆ.push).to_formula âŸ· (ğ—Ÿ p âŸ¶ ğ—  Ïˆ.to_formula),
      by simpa using equiv_to_formula_imply ğ—Ÿ'T Ï†.dummy.push Ïˆ.push,
      have : T âŠ¢ âˆƒ'ğ—¡ (Ï†.mlift.imply Ïˆ.push).to_formula âŸ· âˆƒ'(ğ—— p âŸ¶ Ïˆ.to_formula),
      by simpa using equiv_exists_of_equiv' this,
      refine equiv_trans this (equiv_symm $ imply_exists_pnf _ _)
    end
| m T (fal Ï†)            Ïˆ                  :=
    begin
      simp, show T âŠ¢ (âˆƒ'ğ—¡ (Ï†.push.imply Ïˆ.mlift).to_formula) âŸ· (âˆ€'Ï†.to_formula âŸ¶ Ïˆ.to_formula),
      have : ğ—Ÿ'T âŠ¢ (Ï†.push.imply Ïˆ.mlift).to_formula âŸ· (ğ—  Ï†.to_formula âŸ¶ ğ—Ÿ Ïˆ.to_formula),
      by simpa using equiv_to_formula_imply ğ—Ÿ'T Ï†.push Ïˆ.mlift,
      have : T âŠ¢ âˆƒ'ğ—¡ (Ï†.push.imply Ïˆ.mlift).to_formula âŸ· âˆƒ'(Ï†.to_formula âŸ¶ ğ—— Ïˆ.to_formula),
      by simpa using equiv_exists_of_equiv' this,
      refine equiv_trans this (equiv_symm $ forall_imply_pnf _ _)
    end
| m T (ex Ï†)             Ïˆ                  :=
    begin
      simp, show T âŠ¢ (âˆ€'ğ—¡ (Ï†.push.imply Ïˆ.mlift).to_formula) âŸ· (âˆƒ'Ï†.to_formula âŸ¶ Ïˆ.to_formula),
      have : ğ—Ÿ'T âŠ¢ (Ï†.push.imply Ïˆ.mlift).to_formula âŸ· (ğ—  Ï†.to_formula âŸ¶ ğ—Ÿ Ïˆ.to_formula),
      by simpa using equiv_to_formula_imply ğ—Ÿ'T Ï†.push Ïˆ.mlift,
      have : T âŠ¢ âˆ€'ğ—¡ (Ï†.push.imply Ïˆ.mlift).to_formula âŸ· âˆ€'(Ï†.to_formula âŸ¶ ğ—— Ïˆ.to_formula),
      by simpa using equiv_forall_of_equiv' this,
      refine equiv_trans this (equiv_symm $ exists_imply_pnf _ _)
    end
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.2.1.rank + x.2.2.2.rank)âŸ©]}

instance : has_logic_symbol (pnf L m n) :=
logic_simbol_default (pnf L m n) (openformula âŠ¤ (by simp)) neg imply

end pnf

namespace subformula
open pnf axiomatic_classical_logic' axiomatic_classical_logic provable
variables {L m n} (T : preTheory L m)

@[simp] def to_pnf : Î  {m n}, subformula L m n â†’ pnf L m n
| m n verum          := openformula âŠ¤ (by simp)
| m n (relation r v) := openformula (relation r v) (by simp)
| m n (equal t u)    := openformula (t =' u) (by simp)
| m n (imply p q)    := (to_pnf p).imply (to_pnf q)
| m n (neg p)        := (to_pnf p).neg
| m n (fal p)        := âˆ€'pnf.pull (to_pnf (ğ—  p))
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.2.complexity)âŸ©]}

def normalize (p : subformula L m n) : subformula L m n := p.to_pnf.to_formula

@[simp] lemma to_pnf_top : to_pnf (âŠ¤ : subformula L m n) = openformula âŠ¤ (by simp) := by unfold has_top.top; simp; refl

@[simp] lemma to_pnf_equal (t u) : to_pnf (t =' u : subformula L m n) = openformula (t =' u) (by simp) :=
by unfold has_eq.eq; simp; refl

@[simp] lemma to_pnf_imply (p q : subformula L m n) : to_pnf (p âŸ¶ q) = (to_pnf p).imply (to_pnf q) :=
by unfold has_arrow.arrow; simp; refl

@[simp] lemma to_pnf_neg (p : subformula L m n) : to_pnf (âˆ¼p) = (to_pnf p).neg :=
by unfold has_negation.neg; simp; refl

@[simp] lemma to_pnf_fal (p : subformula L m (n + 1)) : to_pnf (âˆ€'p) = âˆ€' pnf.pull (to_pnf $ ğ—  p) :=
by unfold has_univ_quantifier'.univ; simp; refl

end subformula

section 
open pnf subformula axiomatic_classical_logic' axiomatic_classical_logic provable

lemma equiv_normalize : âˆ€ {m} (T : preTheory L m) (p), T âŠ¢ normalize p âŸ· p
| m T verum          := by simp[top_eq, normalize]
| m T (relation r v) := by simp[normalize]
| m T (equal t u)    := by simp[equal_eq, normalize]
| m T (imply p q)    := by {
    simp[imply_eq, normalize],
    have : T âŠ¢ (p.to_pnf.imply q.to_pnf).to_formula âŸ· (p.normalize âŸ¶ q.normalize),
    from equiv_to_formula_imply T p.to_pnf q.to_pnf,
    exact equiv_trans this (equiv_imply_of_equiv (equiv_normalize T p) (equiv_normalize T q)) }
| m T (neg p)        := by { 
    simp[neg_eq, normalize],
    have : T âŠ¢ p.to_pnf.neg.to_formula âŸ· âˆ¼p.normalize, from equiv_to_formula_neg T p.to_pnf,
    exact equiv_trans this (equiv_neg_of_equiv (equiv_normalize T p)) }
| m T (fal p)        := by { 
    simp[fal_eq, normalize],
    have : ğ—Ÿ'T âŠ¢ (ğ—  p).normalize âŸ· ğ—  p, by simpa using equiv_normalize ğ—Ÿ'T p.push,
    exact equiv_forall_of_equiv (by simpa using this) }
using_well_founded {rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» x, x.2.2.complexity)âŸ©]}

end 

private def s : subformula language.empty 1 0 := (&0 =' &0) âŸ¶ âˆ€'((#0 =' &0) âŠ“ âˆƒ'(#1 =' #2) âŠ” âˆ€' âˆƒ' ((#0 =' #1) âŸ· (#0 =' &0)))

#eval to_string s
#eval to_string s.to_pnf
#eval to_string s.normalize

end fol